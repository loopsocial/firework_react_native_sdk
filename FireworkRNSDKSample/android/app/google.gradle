// google firebase
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'

android {

  buildTypes {

    debug {
      def debugNotesTxt = "build/$project.name-releasenotes-debug.txt"
      firebaseAppDistribution {
        if (project.file(debugNotesTxt).exists()) {
          releaseNotesFile= project.file(debugNotesTxt)
        } else {
          releaseNotes=""
        }
        groups="email-notification"
        serviceCredentialsFile=project.hasProperty('FIREBASE_CREDENTIAL_FILE_PATH') ? FIREBASE_CREDENTIAL_FILE_PATH : ""
      }
    }

    release {
      def releaseNotesTxt = "build/$project.name-releasenotes-release.txt"
      firebaseAppDistribution {
        if (project.file(releaseNotesTxt).exists()) {
          releaseNotesFile= project.file(releaseNotesTxt)
        } else {
          releaseNotes=""
        }
        groups="email-notification"
        serviceCredentialsFile=project.hasProperty('FIREBASE_CREDENTIAL_FILE_PATH') ? FIREBASE_CREDENTIAL_FILE_PATH : ""
      }
    }
  }
}

dependencies {
  // Import the BoM for the Firebase platform
  implementation platform('com.google.firebase:firebase-bom:29.0.3')

  // Declare the dependencies for the Crashlytics and Analytics libraries
  // When using the BoM, you don't specify versions in Firebase library dependencies
  implementation 'com.google.firebase:firebase-crashlytics-ktx'
  implementation 'com.google.firebase:firebase-analytics-ktx'
}

afterEvaluate {
  try {
    appDistributionUploadReleaseIfNeeded.dependsOn appDistributionUploadRelease
    appDistributionUploadDebugIfNeeded.dependsOn appDistributionUploadDebug

    appDistributionUploadDebug.dependsOn processDebugGoogleServices
    appDistributionUploadRelease.dependsOn processReleaseGoogleServices
    appDistributionUploadIfNeeded.dependsOn appDistributionUploadReleaseIfNeeded
    appDistributionUploadIfNeeded.dependsOn appDistributionUploadDebugIfNeeded
  } catch (Exception e) {
    e.printStackTrace()
  }
}

task appDistributionUploadIfNeeded()

task appDistributionUploadDebugIfNeeded()

task appDistributionUploadReleaseIfNeeded()
